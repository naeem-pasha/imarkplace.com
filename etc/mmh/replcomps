%; $Header$
%;
%; These next lines slurp in lots of addresses for To: and Cc:.
%; Use with repl -query or else you may get flooded with addresses!
%;
%; If no To:/Cc:/Fcc: text, we output empty fields for prompter to fill in.
%;
%(lit)%(formataddr{reply-to})\
%(formataddr %<{from}%(void{from})%|%(void{apparently-from})%>)\
%(formataddr{resent-to})\
%(formataddr{prev-resent-to})\
%(formataddr{x-to})\
%(formataddr{apparently-to})\
%(void(width))%(void(decode))%(putaddr To: )
%(lit)%(formataddr{to})\
%(formataddr{cc})\
%(formataddr{x-cc})\
%(formataddr{resent-cc})\
%(formataddr{prev-resent-cc})\
%(formataddr(me))\
%(void(width))%(void(decode))%(putaddr Cc: )
Fcc: +sent
Subject: %<{subject}Re: %(putstr(trim(decode{subject})))%>
%;
%; Make References: and In-reply-to: fields for threading.
%; Use (trim) to eat trailing whitespace.
%;
%<{message-id}In-reply-to: %{message-id}\n%>\
%<{message-id}References: \
%<{references}%(trim{references})%(putstr) %>\
%(trim{message-id})%(putstr)\n%>\
Comments: In-reply-to \
%<{from}%(void{from})%?(void{apparently-from})%|%(void{sender})%>\
%(putstr(trim(decode)))\n\
   message dated "%<(nodate{date})%{date}%|%(tws{date})%>."
--------
[%4(year{date})-%02(mon{date})-%02(mday{date}) \
%02(hour{date}):%02(min{date})] %(decode{from})
>
