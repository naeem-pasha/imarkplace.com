%; repllistcomps
%;
%; form (components) file for mailing list replies
%;
%; Check the following headers to create reply addresses.
%;
%; To: List-Post
%;
%; OR
%;
%; To: Mail-Followup-To
%;
%; OR
%;
%; To: Mail-Reply-To   (or)
%;     Reply-To        (or)
%;     From            (or)
%;     Sender          (or)
%;     Return-Path
%;
%; AND
%;
%; Cc: To              (and)
%;     Cc              (and)
%;     personal address
%;
%(lit)%(formataddr(unmailto{list-post}))\
%<(nonnull)%(void(width))%(void(decode))%(putaddr To: )\n\
%|\
%(lit)%(formataddr{mail-followup-to})\
%<(nonnull)%(void(width))%(void(decode))%(putaddr To: )\n\
%|\
%(lit)%(formataddr %<{mail-reply-to}%?{reply-to}%?{from}%?{sender}%?{return-path}%>)\
%<(nonnull)%(void(width))%(void(decode))%(putaddr To: )\n%>\
%(lit)%(formataddr{to})%(formataddr{cc})%(formataddr(me))\
%<(nonnull)%(void(width))%(void(decode))%(putaddr Cc: )\n%>\
%>\
%>\
%;
Fcc: +sent
Subject: %<{subject}Re: %(putstr(trim(decode{subject})))%>
%;
%; Make References: and In-reply-to: fields for threading.
%; Use (trim) to eat trailing whitespace.
%;
%<{message-id}In-reply-to: %{message-id}\n%>\
%<{message-id}References: \
%<{references}%(trim{references})%(putstr) %>\
%(trim{message-id})%(putstr)\n%>\
Comments: In-reply-to \
%<{from}%(void{from})%?(void{apparently-from})%|%(void{sender})%>\
%(putstr(trim(decode)))\n\
   message dated "%<(nodate{date})%{date}%|%(tws{date})%>."
--------
[%4(year{date})-%02(mon{date})-%02(mday{date}) \
%02(hour{date}):%02(min{date})] %(decode{from})
>
